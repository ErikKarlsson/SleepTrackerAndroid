apply plugin: 'com.android.application'

apply plugin: 'io.fabric'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'

apply from: "$project.rootDir/git-version.gradle"

def keystorePropertiesFile = new File(System.getProperty("user.home") + '/android/files/simplesleeptracker/keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion Versions.compile_sdk

    defaultConfig {
        applicationId "net.erikkarlsson.simplesleeptracker"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "${applicationId}.runner.TestRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storeFile file(keystoreProperties['debugStoreFile'])
            storePassword keystoreProperties['debugStorePassword']
        }
    }

    buildTypes {
        debug {
            crunchPngs false
            ext.alwaysUpdateBuildId = false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            applicationIdSuffix = ".dev"
            versionCode 1
            versionName "dev"
            resConfigs "en", "xxhdpi"
        }
        internal {
            versionCode gitVersionCodeTime
            versionName gitVersionName
        }
        playstore {
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }

    kapt {
        javacOptions {
            option("-Xmaxerrs", 500)
        }
    }

    sourceSets {
        String sharedTestJavaDir = 'src/sharedTest/java'

        test {
            java.srcDir sharedTestJavaDir
        }
        androidTest {
            java.srcDir sharedTestJavaDir
        }
    }

    lintOptions {
        lintConfig rootProject.file("code-quality/lint.xml")
        abortOnError true
        warningsAsErrors true
        quiet false
        showAll true
        explainIssues true
        textReport true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

configurations.all { config ->

    // Ensure the LeakCanary no-op dependency is always used in JVM and Android tests.
    if (config.name.contains('UnitTest') || config.name.contains('AndroidTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }

    // Force version
    resolutionStrategy.force Deps.kotlin_stdlib
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Dependencies for Android tests
    androidTestImplementation Deps.atsl_runner
    androidTestImplementation Deps.atsl_rules
    androidTestImplementation Deps.room_testing
    androidTestImplementation Deps.arch_core_testing
    androidTestImplementation Deps.junit
    androidTestImplementation Deps.mockito_core, { exclude group: 'net.bytebuddy' }
    androidTestImplementation Deps.dexmaker
    androidTestImplementation Deps.espresso_core
    androidTestImplementation Deps.espresso_contrib
    androidTestImplementation Deps.espresso_intents
    androidTestImplementation Deps.daggermock_android
    androidTestImplementation Deps.daggermock_kotlin
    androidTestImplementation Deps.dagger_runtime
    androidTestImplementation Deps.dagger_android
    androidTestImplementation Deps.dagger_android_support
    kaptAndroidTest Deps.dagger_compiler
    kaptAndroidTest Deps.dagger_android_support_compiler

    // Dependencies for local unit tests
    testImplementation Deps.junit
    testImplementation Deps.hamcrest
    testImplementation Deps.mockito_kotlin
    testImplementation Deps.arch_core_testing
    testImplementation Deps.mockito_inline

    // Crashlytics
    implementation Deps.crashlytics

    // Dagger
    implementation Deps.dagger_runtime
    implementation Deps.dagger_android
    implementation Deps.dagger_android_support
    kapt Deps.dagger_compiler
    kapt Deps.dagger_android_support_compiler

    // Fast CSV
    implementation Deps.fast_csv

    // Firebase
    implementation Deps.firebase_core

    // Glide
    implementation Deps.glide
    kapt Deps.glide_compiler

    // Guava
    implementation Deps.guava

    // Jsr 305
    implementation Deps.jsr305

    // Kotlin
    implementation Deps.kotlin_stdlib
    implementation Deps.core_ktx

    // LeakCanary
    debugImplementation Deps.leakcanary
    releaseImplementation Deps.leakcanary_no_op

    // Lifecycle
    implementation Deps.lifecycle_extensions
    kapt Deps.lifecycle_compiler

    // Material
    implementation Deps.material

    // Material progress bar
    implementation Deps.materialprogressbar

    // MPAndroidChart
    implementation Deps.mpandroidchart

    // Multidex
    implementation Deps.multidex
    implementation Deps.multidex_instrumentation

    // Navigation
    implementation Deps.navigation_fragment
    implementation Deps.navigation_ui
    androidTestImplementation Deps.navigation_testing

    // Paging
    implementation Deps.paging
    implementation Deps.paging_rx

    // Play Services
    implementation Deps.play_services_auth
    implementation Deps.play_services_drive

    // Room
    implementation Deps.room_runtime
    implementation Deps.room_rxjava2
    kapt Deps.room_compiler

    // RxJava
    implementation Deps.rxjava2
    implementation Deps.rxandroid
    implementation Deps.rxrelay
    implementation Deps.rxbinding
    implementation Deps.rxkotlin
    implementation Deps.rx_preferences

    // Support libraries
    implementation Deps.support_app_compat
    implementation Deps.support_cardview
    implementation Deps.support_constraint_layout
    implementation Deps.support_v4
    implementation Deps.support_vector_drawable

    // ThreeTen Backport
    implementation Deps.threeten_backport_android
    testImplementation Deps.threeten_backport

    // Timber
    implementation Deps.timber

    // Work Manager
    // TODO (erikkarlsson): Exclude fixes bug in alpha09 https://stackoverflow.com/questions/52467819/compile-errors-after-updating-to-workmanager-1-0-0-alpha09
    // Remove when updating to stable release.
    implementation(Deps.work_manager_runtime) {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    implementation(Deps.work_manager_firebase) {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    androidTestImplementation Deps.work_manager_testing
}

apply plugin: 'com.google.gms.google-services'
