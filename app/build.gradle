apply plugin: 'com.android.application'

apply plugin: 'com.google.android.gms.oss-licenses-plugin'

apply plugin: 'io.fabric'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'

apply from: "$project.rootDir/git-version.gradle"

def keystorePropertiesFile = new File(System.getProperty("user.home") + '/android/files/simplesleeptracker/keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    defaultConfig {
        applicationId "net.erikkarlsson.simplesleeptracker"
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storeFile file(keystoreProperties['debugStoreFile'])
            storePassword keystoreProperties['debugStorePassword']
        }
    }

    buildTypes {
        debug {
            crunchPngs false
            ext.alwaysUpdateBuildId = false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            applicationIdSuffix = ".dev"
            versionCode 1
            versionName "dev"
            resConfigs "en", "xxhdpi"
        }
        internal {
            versionCode gitVersionCodeTime
            versionName gitVersionName
        }
        playstore {
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }

    kapt {
        javacOptions {
            option("-Xmaxerrs", 500)
        }
    }

    lintOptions {
        lintConfig rootProject.file("code-quality/lint.xml")
        abortOnError true
        warningsAsErrors true
        quiet false
        showAll true
        explainIssues true
        textReport true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    compileOptions {
        sourceCompatibility Config.javaVersion
        targetCompatibility Config.javaVersion
    }
}

configurations.all { config ->

    // Ensure the LeakCanary no-op dependency is always used in JVM and Android tests.
    if (config.name.contains('UnitTest') || config.name.contains('AndroidTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }

    // Force version
    resolutionStrategy.force Deps.kotlin_stdlib
    resolutionStrategy.force Deps.threeten_backport
    resolutionStrategy.force Deps.core_ktx
    resolutionStrategy.force Deps.gson

    // https://stackoverflow.com/questions/46989310/commons-logging-defines-classes-that-conflict-with-classes-now-provided-by-andro
    exclude module: 'httpclient'
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':features:home')
    implementation project(':features:diary')
    implementation project(':features:details')
    implementation project(':features:add')
    implementation project(':features:settings')
    implementation project(':features:statistics')
    implementation project(':features:backup')
    implementation project(':features:appwidget')
    implementation project(':libraries:core')
    implementation project(':libraries:domain')
    implementation project(':libraries:data')
    implementation project(':libraries:navigation')
    implementation project(':libraries:ui-components')

    // Dependencies for Android tests
    androidTestImplementation project(':libraries:testutilsnew')

    androidTestImplementation Deps.atsl_runner
    androidTestImplementation Deps.atsl_rules
    androidTestImplementation Deps.room_testing
    androidTestImplementation Deps.arch_core_testing
    androidTestImplementation Deps.junit
    androidTestImplementation Deps.mockito_core, { exclude group: 'net.bytebuddy' }
    androidTestImplementation Deps.dexmaker
    androidTestImplementation Deps.espresso_core
    androidTestImplementation Deps.espresso_contrib
    androidTestImplementation Deps.espresso_intents
    androidTestImplementation Deps.daggermock_android
    androidTestImplementation Deps.daggermock_kotlin
    androidTestImplementation Deps.dagger_runtime
    androidTestImplementation Deps.dagger_android
    androidTestImplementation Deps.dagger_android_support
    kaptAndroidTest Deps.dagger_compiler
    kaptAndroidTest Deps.dagger_android_support_compiler

    // Crashlytics
    implementation Deps.crashlytics

    // Dagger
    implementation Deps.dagger_runtime
    implementation Deps.dagger_android
    implementation Deps.dagger_android_support
    implementation Deps.dagger_assisted_injection_annotations
    kapt Deps.dagger_compiler
    kapt Deps.dagger_android_support_compiler
    kapt Deps.dagger_assisted_injection_processor

    // Google Drive REST API
    implementation Deps.google_http_client_gson
    implementation(Deps.google_api_client_android) {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation(Deps.google_api_services_drive) {
        exclude group: 'org.apache.httpcomponents'
    }

    // Kotlin
    implementation Deps.kotlin_stdlib
    implementation Deps.core_ktx

    implementation Deps.coroutines_core
    implementation Deps.coroutines_android

    // Guava
    implementation Deps.guava

    // LeakCanary
    debugImplementation Deps.leakcanary
    releaseImplementation Deps.leakcanary_no_op

    // Lifecycle
    implementation Deps.lifecycle_extensions
    kapt Deps.lifecycle_compiler

    // Material progress bar
    implementation Deps.materialprogressbar

    // MPAndroidChart
    implementation Deps.mpandroidchart

    // Multidex
    implementation Deps.multidex
    implementation Deps.multidex_instrumentation

    // MvRx
    implementation Deps.mvrx

    // Navigation
    implementation Deps.navigation_fragment
    implementation Deps.navigation_ui
    androidTestImplementation Deps.navigation_testing

    // Play Services
    implementation Deps.play_services_auth
    implementation Deps.play_services_oss_licenses

    // RxJava
    implementation Deps.rxjava2
    implementation Deps.rxandroid
    implementation Deps.rxbinding
    implementation Deps.rxkotlin

    // Flow
    implementation Deps.flow_preferences

    // ThreeTen Backport
    implementation Deps.threeten_backport_android
    testImplementation Deps.threeten_backport
    androidTestImplementation Deps.threeten_backport

    // Timber
    implementation Deps.timber

    // Work Manager
    // TODO (erikkarlsson): Exclude fixes bug in alpha09 https://stackoverflow.com/questions/52467819/compile-errors-after-updating-to-workmanager-1-0-0-alpha09
    // Remove when updating to stable release.
    implementation(Deps.work_manager_runtime) {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    androidTestImplementation Deps.work_manager_testing
}

apply plugin: 'com.google.gms.google-services'
