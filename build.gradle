buildscript {

    dependencies {
        classpath Deps.android_gradle_plugin
        classpath Deps.fabric_plugin
        classpath Deps.google_services_plugin
        classpath Deps.kotlin_plugin
        classpath Deps.navigation_plugin
        classpath Deps.play_services_oss_licenses_plugin
    }

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://maven.fabric.io/public' }
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-4"
    id "com.github.ben-manes.versions" version "0.28.0"
}

detekt {
    version = "1.0.0.RC6-4"
    profile("main") {
        input = "$projectDir/app/src/main/java"
        config = "$projectDir/code-quality/detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/app/build/reports"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "com.github.ben-manes.versions"

            androidExtensions {
                experimental = true
            }

            android {
                compileSdkVersion Config.compileSdk

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                    testInstrumentationRunner "${applicationId}.runner.TestRunner"
                    vectorDrawables.useSupportLibrary = true
                }

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }

                kotlinOptions {
                    jvmTarget = Config.javaVersion
                }

                flavorDimensions "default"

                productFlavors {
                    dev {
                    }
                    internal {
                    }
                    playstore {
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
