buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath Deps.grgit_plugin
    }
}

/**
 * git.describe()
 *
 * Find the most recent tag that is reachable from HEAD. If the tag points to the commit,
 * then only the tag is shown. Otherwise, it suffixes the tag name with the number of additional
 * commits on top of the tagged object and the abbreviated object name of the most recent commit.
 *
 * More info:
 * https://git-scm.com/docs/git-describe
 * https://proandroiddev.com/configuring-android-project-version-name-code-b168952f3323
 */

import org.ajoberstar.grgit.Grgit

ext {
    git = Grgit.open(currentDir: projectDir)
    gitVersionName = git.describe()
    gitVersionCode = git.tag.list().size()
    gitVersionCodeTime = git.head().time.toInteger()
}

task printVersion() {
    doLast {
        println("Version Name: $gitVersionName")
        println("Version Code: $gitVersionCode")
        println("Version Code Time: $gitVersionCodeTime")
    }
}
