apply plugin: 'com.android.application'

apply plugin: 'com.google.android.gms.oss-licenses-plugin'

apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'

apply plugin: 'com.facebook.testing.screenshot'

apply plugin: 'shot'

apply plugin: 'dagger.hilt.android.plugin'

apply from: "$project.rootDir/git-version.gradle"

def keystorePropertiesFile = new File(System.getProperty("user.home") + '/android/files/simplesleeptracker/keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    defaultConfig {
        applicationId "net.erikkarlsson.simplesleeptracker"
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storeFile file(keystoreProperties['debugStoreFile'])
            storePassword keystoreProperties['debugStorePassword']
        }
    }

    buildTypes {
        debug {
            crunchPngs false
            signingConfig signingConfigs.debug

            firebaseCrashlytics {
                // If you don't need crash reporting for your debug build,
                // you can speed up your build by disabling mapping file uploading.
                mappingFileUploadEnabled false
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            applicationIdSuffix = ".dev"
            versionCode 1
            versionName "dev"
            resConfigs "en", "xxhdpi"
        }
        internal {
            versionCode gitVersionCodeTime
            versionName gitVersionName
        }
        playstore {
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }

    kapt {
        correctErrorTypes true

        javacOptions {
            option("-Xmaxerrs", 500)
        }
    }

    lintOptions {
        lintConfig rootProject.file("code-quality/lint.xml")
        abortOnError true
        warningsAsErrors true
        quiet false
        showAll true
        explainIssues true
        textReport true
    }

    compileOptions {
        sourceCompatibility Config.javaVersion
        targetCompatibility Config.javaVersion
    }
}

configurations.all { config ->

    // Force version
    resolutionStrategy.force Deps.kotlin_stdlib
    resolutionStrategy.force Deps.threeten_backport
    resolutionStrategy.force Deps.core_ktx
    resolutionStrategy.force Deps.gson

    // https://stackoverflow.com/questions/46989310/commons-logging-defines-classes-that-conflict-with-classes-now-provided-by-andro
    exclude module: 'httpclient'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':features:home')
    implementation project(':features:diary')
    implementation project(':features:details')
    implementation project(':features:add')
    implementation project(':features:settings')
    implementation project(':features:statistics')
    implementation project(':features:backup')
    implementation project(':features:appwidget')
    implementation project(':libraries:core')
    implementation project(':libraries:domain')
    implementation project(':libraries:data')
    implementation project(':libraries:navigation')
    implementation project(':libraries:ui-components')

    androidTestImplementation project(':libraries:test-util')

    androidTestImplementation Deps.atsl_runner
    androidTestImplementation Deps.atsl_rules
    androidTestImplementation Deps.room_testing
    androidTestImplementation Deps.arch_core_testing
    androidTestImplementation Deps.junit
    androidTestImplementation Deps.junit_ext
    androidTestImplementation Deps.mockito_core, { exclude group: 'net.bytebuddy' }
    androidTestImplementation Deps.dexmaker
    androidTestImplementation Deps.espresso_core
    androidTestImplementation Deps.espresso_contrib
    androidTestImplementation Deps.espresso_intents
    androidTestImplementation Deps.core_test_ktx
    androidTestImplementation Deps.junit_ktx

    implementation Deps.crashlytics

    implementation Deps.dagger_assisted_injection_annotations
    kapt Deps.dagger_assisted_injection_processor

    implementation Deps.hilt_work
    implementation Deps.dagger_hilt
    kapt Deps.dagger_hilt_compiler

    androidTestImplementation Deps.dagger_hilt_testing
    kaptAndroidTest Deps.dagger_hilt_testing_compiler

    implementation Deps.google_http_client_gson
    implementation(Deps.google_api_client_android) {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation(Deps.google_api_services_drive) {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation Deps.kotlin_stdlib
    implementation Deps.core_ktx

    implementation Deps.coroutines_core
    implementation Deps.coroutines_android

    implementation Deps.guava

    debugImplementation Deps.leakcanary
    androidTestImplementation Deps.leakcanary_instrumentation

    implementation Deps.lifecycle_extensions
    kapt Deps.lifecycle_compiler

    implementation Deps.materialprogressbar

    implementation Deps.mpandroidchart

    implementation Deps.multidex
    implementation Deps.multidex_instrumentation

    implementation Deps.navigation_fragment
    implementation Deps.navigation_ui
    androidTestImplementation Deps.navigation_testing

    implementation Deps.play_services_auth
    implementation Deps.play_services_oss_licenses

    implementation Deps.flow_preferences

    implementation Deps.threeten_backport_android
    testImplementation Deps.threeten_backport
    androidTestImplementation Deps.threeten_backport

    implementation Deps.timber

    implementation Deps.work_manager_runtime

    androidTestImplementation Deps.work_manager_testing
}

apply plugin: 'com.google.gms.google-services'
